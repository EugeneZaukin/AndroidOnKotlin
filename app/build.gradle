plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'

}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.eugene.androidonkotlin"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    //Pulling key from file
    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("apikey.properties.txt").newDataInputStream())
        def apiKey = properties.getProperty("my_api_key", "")
        it.buildConfigField "String", "MY_API_KEY", apiKey
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "TYPE", '"RELEASE"'
        }

        beta {
            initWith debug
            versionNameSuffix ".beta"
            buildConfigField "String", "TYPE", '"BETA"'
        }

        debug {
            versionNameSuffix ".debug"
            buildConfigField "String", "TYPE", '"DEBUG"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions 'version'
    productFlavors {
        freeConfig {
            dimension 'version'
            versionNameSuffix '.free'
        }

        paid {
            dimension 'version'
            versionNameSuffix '.paid'
        }
    }
}

dependencies {

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    //Testing
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //Room
    implementation 'androidx.room:room-runtime:2.4.1'
    implementation 'com.google.firebase:firebase-messaging:23.0.0'
    kapt 'androidx.room:room-compiler:2.4.1'

    //RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

    //Fast Adapter
    implementation "com.mikepenz:fastadapter:5.5.1"
    implementation "com.mikepenz:fastadapter-extensions-binding:5.5.1"
    implementation "com.mikepenz:fastadapter-extensions-diff:5.5.1"

    //Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
}


apply plugin: 'com.google.gms.google-services'